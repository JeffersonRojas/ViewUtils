/*
 * Copyright 2017 Jefferson Rojas
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionCode 3
        versionName "1.0.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath +=project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.github.jeffersonrojas'
version = '1.0.2'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'viewutils'
            description 'A library for android which helps you to custom a some views more easily.'
            url 'https://github.com/JeffersonRojas/ViewUtils'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.github.jeffersonrojas'
            artifactId 'viewutils'
            version '1.0.2'

            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id  'JeffersonRojas'
                    name 'Jefferson Rojas'
                    email 'fejerson108@gmail.com'
                }
            }
            scm {
                connection 'https://github.com/JeffersonRojas/ViewUtils.git'
                developerConnection 'JeffersonRojas/ViewUtils'
                url 'https://github.com/JeffersonRojas/ViewUtils'

            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('apikey')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'viewutils'
        userOrg = bintrayProperties.getProperty('bintray.user')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JeffersonRojas/ViewUtils.git'
        publish = true
        version {
            name = '1.0.2'
            desc = 'A library for android which helps you to custom a some views more easily.'
            released = new Date()
            vcsTag = '1.0.2'
        }
    }
}